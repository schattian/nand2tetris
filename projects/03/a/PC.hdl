// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Mux16(a=false, b[0]=true, sel=inc, out=delta);
    Add16(a=prevOut, b=delta, out=incOut);
    Mux16(a=incOut, b=in, sel=load, out=jmpOut);
    Mux16(a=jmpOut, b=false, sel=reset, out=resetOut);

    Register(in=resetOut, load=true, out=prevOut, out=out);
}
